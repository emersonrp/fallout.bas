10 CLS
20 RANDOM
30 PRINT
40 PRINT "Welcome to ROBCO Industries (TM) Termlink"
50 PRINT
60 PRINT "Password Required"
70 PRINT
75 REM - TODO make sub to print CHR$(16) + " " + CHR$(17) + " " for each remaining attempt
80 PRINT "Attempts Remaining: "
90 PRINT
900 REM - get the words - TODO need more word sets
910 DATA TAKES,KNOWN,KICKS,STARK,BOOTS,BATON,CLEAR,CRIME,WASTE,CLOSE,SWORD,SLAVE,FARGO,MAYBE,MALES
920 DIM WORDS$(15)
930 FOR I = 1 TO 15
940 READ WORDS$(I)
950 NEXT
960 WORDLENGTH% = LEN(WORDS$(1))
1000 REM - set up the vars representing the left and right text fields
1010 DATA " ","/","\","!","@","#","$","%","^","&","*","-","=","+",",",".",":",";"
1020 DIM GARBAGECHARS$(18)
1030 FOR I = 1 to 18
1040 READ GARBAGECHARS$(I)
1050 NEXT
1060 FOR I = 1 TO 12*16
1070 GOSUB 40000
1080 LSIDE$ = LSIDE$ + NEWCHAR$
1090 NEXT
1100 FOR I = 1 to 12*16
1110 GOSUB 40000
1120 RSIDE$ = RSIDE$ + NEWCHAR$
1130 NEXT I
1500 REM - sprinkle the words into the text fields
1505 DIM PAPOSITIONS%(15,WORDLENGTH%) ' (number of words, length) TODO put those in vars
1510 DATA 14,50,66,95,108,128,154,174
1520 FOR I = 1 TO 8
1530 READ LSIDEPOS%(I)
1540 LSIDEPOS%(I) = LSIDEPOS%(I) + RND(7) - 4
1550 MID$(LSIDE$, LSIDEPOS%(I), LEN(WORDS$(I))) = WORDS$(I)
1555 GOSUB 41000
1560 NEXT
1570 DATA 18,40,66,85,100,138,164
1580 FOR I = 1 TO 7
1590 READ RSIDEPOS%(I)
1600 RSIDEPOS%(I) = RSIDEPOS%(I) + RND(7) - 4
1610 MID$(RSIDE$, RSIDEPOS%(I), LEN(WORDS$(I+8))) = WORDS$(I+8)
1615 GOSUB 42000
1620 NEXT
10000 REM - print the text fields at the right place
10010 TOPROW = 7
10020 HEXADDRESS = 48304
10030 FOR I = 0 TO 15
10040 PRINT @ (TOPROW + I, 0), "0x" + HEX$(HEXADDRESS) + " " + MID$(LSIDE$, I*12 + 1, 12) + " 0x" + HEX$(HEXADDRESS + (12*16)) + " " + MID$(RSIDE$, I*12 + 1, 12);
10050 HEXADDRESS = HEXADDRESS + 12
10060 NEXT I
10070 PRINT @ (TOPROW + 15, 40), " > ";
20000 REM - Set up the first word as our current highlighted one.
20010 CURRWORD% = 1
20020 GOSUB 43000
21000 REM - Do the input loop and dispatch appropriately
21020 KEYIN$ = INPUT$(1)
21030 IF KEYIN$ = CHR$(10) THEN GOSUB 44000: GOSUB 45000: GOSUB 43000
21040 IF KEYIN$ = CHR$(11) THEN GOSUB 44000: GOSUB 46000: GOSUB 43000
21999 GOTO 21020
39999 END
40000 REM --- subroutine to pick a more random char from garbagechars
40010 NEWCHAR$ = GARBAGECHARS$(RND(18))
40020 IF NEWCHAR$ = LASTCHAR$ THEN 40010
40030 LASTCHAR$ = NEWCHAR$
40040 RETURN
40999 REM TODO TODO TODO DRY UP THESE NEXT TWO
41000 REM -- subroutine to fill a two-dimensional array with lists of "print @" locations for LSIDE
41010 RAWPOS% = LSIDEPOS%(I)
41020 CURROW% = (RAWPOS% \ 12) + 7
41030 CURCOL% = (RAWPOS% MOD 12)
41035 IF CURCOL% = 0 THEN CURCOL% = 12: CURROW% = CURROW% - 1
41040 FOR J = 1 to WORDLENGTH%
41050 PAPOSITIONS%(I, J) = (CURROW% * 80) + 6 + CURCOL%
41060 CURCOL% = CURCOL% + 1
41070 IF CURCOL% > 12 THEN CURCOL% = 1: CURROW% = CURROW% + 1
41080 NEXT J
41090 RETURN
42000 REM -- subroutine to fill a two-dimensional array with lists of "print @" locations for RSIDE
42010 RAWPOS% = RSIDEPOS%(I)
42020 CURROW% = (RAWPOS% \ 12) + 7
42030 CURCOL% = (RAWPOS% MOD 12)
42035 IF CURCOL% = 0 THEN CURCOL% = 12: CURROW% = CURROW% - 1
42040 FOR J = 1 to WORDLENGTH%
42050 PAPOSITIONS%(I+8, J) = (CURROW% * 80) + 26 + CURCOL%
42060 CURCOL% = CURCOL% + 1
42070 IF CURCOL% > 12 THEN CURCOL% = 1: CURROW% = CURROW% + 1
42080 NEXT J
42090 RETURN
43000 REM - highlight the word pointed to by CURRWORD% and put it lower-right
43005 PRINT @ (TOPROW + 15, 43), STRING$(WORDLENGTH%, " ");
43010 FOR J = 1 to WORDLENGTH%
43020 PRINT @ PAPOSITIONS%(CURRWORD%, J), CHR$(16) + MID$(WORDS$(CURRWORD%), J, 1) + CHR$(17);
43030 PRINT @ (TOPROW + 15, 42 + J), MID$(WORDS$(CURRWORD%), J, 1);
43040 NEXT J
43050 RETURN
44000 REM - UN-highlight the word pointed to by CURRWORD%
44010 FOR J = 1 to WORDLENGTH%
44020 PRINT @ PAPOSITIONS%(CURRWORD%, J), MID$(WORDS$(CURRWORD%), J, 1);
44030 NEXT J
44040 RETURN
45000 REM - Increment CURRWORD%, wrapping around
45010 CURRWORD% = CURRWORD% + 1
45020 IF CURRWORD% = 16 THEN CURRWORD% = 1
45030 RETURN
46000 REM - Decrement CURRWORD%, wrapping around
46010 CURRWORD% = CURRWORD% - 1
46020 IF CURRWORD% = 0 THEN CURRWORD% = 15
46030 RETURN
